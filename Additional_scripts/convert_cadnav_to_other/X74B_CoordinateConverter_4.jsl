//Converts between coordinates for X72B/X74B/1X115B etc. reticle. Columns must be named in the format CAMDEX X or CDSEM X etc. Note that this script cannot convert FROM CADNAV coordinates for multiple seats in the same table. You will have to create a table for each seat and select the appropriate option for that table when running the script, then concatenate the tables afterward. There should however be no issue with conversion TO CADNAV from full reticle coordinates, as this script will make the appropriate seat assignments.

Names Default To Here(1);
dt = Current Data Table();

New Window("X7xB reticle coordinate conversion",
	<< Modal,
		H List Box(
		V List Box(
		Text Box("Coordinates you have (in um):"),
			Convert_from = Combo Box({"CAMDEX", "CDSEM", "KLARITY", "CADNAV (ULV left)", "CADNAV (YLD no DFM left)", "CADNAV (YLD DFM left)", "CADNAV (ULV middle)", "CADNAV (YLD no DFM middle)", "CADNAV (YLD DFM middle)", "CADNAV (ULV right)", "CADNAV (YLD no DFM right)", "CADNAV (YLD DFM right)", "CADNAV (TC11)", "CADNAV (TC12)", "CADNAV (TC13)", "CADNAV (TC21)", "CADNAV (TC22)", "CADNAV (TC23)", "CADNAV (Frame)"})
		),
		V List Box(
		Text Box("Coordinates you want (in um):"),
			Convert_to = Combo Box({"CAMDEX", "CDSEM", "KLARITY", "CADNAV"})
		),
		V List Box(
		Button Box("OK",
			Conversion_1 = Convert_from << get; //1 for CAMDEX; 2 for CDSEM; 3 for KLARITY; 4-6 for CADNAV ULV left, YLD no DFM left, YLD DFM left; 7-9 for CADNAV ULV middle, YLD no DFM middle, YLD DFM middle; 10-12 for CADNAV ULV right, YLD no DFM right, YLD DFM right; 13-15 for CADNAV TC11, TC12, TC13; 16-18 for CADNAV TC21, TC22, TC23; 19 for CADNAV frame
			Conversion_2 = Convert_to << get; //1 for CAMDEX, 2 for CDSEM, 3 for KLARITY, 4 for CADNAV

//CAMDEX coordinate boundaries for columns
Left_column_minX = -12349.344;
Left_column_maxX = -4225.248;
Middle_column_minX = -4062.048;
Middle_column_maxX = 4062.048;
Right_column_minX = 4225.248;
Right_column_maxX = 12349.344;
Frame_minX = -12512.594;
Frame_maxX = 12512.594;

//CAMDEX coordinate boundaries for rows
Bottom_row_minY = -16398.288;
Bottom_row_maxY = -8458.992;
Middle_row_minY = -3969.648;
Middle_row_maxY = 3969.648;
Top_row_minY = 8458.992;
Top_row_maxY = 16398.288;
TC1_row_minY = 4050.000;
TC1_row_maxY = 8378.640;
TC2_row_minY = -8378.640;
TC2_row_maxY = -4050.000;
Frame_minY = -16478.690;
Frame_maxY = 16478.690;

//Offsets from CAMDEX
CDSEM_offset_X = 12425.232;
CDSEM_offset_Y = 16431.984;
KLARITY_offset_X = 12438.172;
KLARITY_offset_Y = 16442.396;

If(
(Conversion_1 == Conversion_2) | 
(4 <=Conversion_1 <= 19 & Conversion_2 == 4 )
,
Print("Conversion to same coordinate system")
,
If(
Conversion_1 == 1, //From CAMDEX
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CAMDEX X")));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CAMDEX Y")));
dt << run formulas;
,
Conversion_1 == 2, //From CDSEM
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CDSEM X") - CDSEM_offset_X));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CDSEM Y") - CDSEM_offset_Y));
dt << run formulas;
,
Conversion_1 == 3, //From KLARITY
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("KLARITY X") - KLARITY_offset_X));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("KLARITY Y") - KLARITY_offset_Y));
dt << run formulas;
,
Conversion_1 == 4, //From CADNAV ULV left
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Left_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Bottom_row_minY));
dt << run formulas;
,
Conversion_1 == 5, //From CADNAV YLD no DFM left
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Left_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Middle_row_minY));
dt << run formulas;
,
Conversion_1 == 6, //From CADNAV YLD DFM left
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Left_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Top_row_minY));
dt << run formulas;
,
Conversion_1 == 7, //From CADNAV ULV middle
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Middle_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Bottom_row_minY));
dt << run formulas;
,
Conversion_1 == 8, //From CADNAV YLD no DFM middle
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Middle_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Middle_row_minY));
dt << run formulas;
,
Conversion_1 == 9, //From CADNAV YLD DFM middle
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Middle_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Top_row_minY));
dt << run formulas;
,
Conversion_1 == 10, //From CADNAV ULV right
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Right_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Bottom_row_minY));
dt << run formulas;
,
Conversion_1 == 11, //From CADNAV YLD no DFM right
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Right_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Middle_row_minY));
dt << run formulas;
,
Conversion_1 == 12, //From CADNAV YLD DFM right
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Right_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Top_row_minY));
dt << run formulas;
,
Conversion_1 == 13, //From CADNAV TC11
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Left_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + TC1_row_minY));
dt << run formulas;
,
Conversion_1 == 14, //From CADNAV TC12
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Middle_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + TC1_row_minY));
dt << run formulas;
,
Conversion_1 == 15, //From CADNAV TC13
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Right_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + TC1_row_minY));
dt << run formulas;
,
Conversion_1 == 16, //From CADNAV TC21
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Left_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + TC2_row_minY));
dt << run formulas;
,
Conversion_1 == 17, //From CADNAV TC22
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Middle_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + TC2_row_minY));
dt << run formulas;
,
Conversion_1 == 18, //From CADNAV TC23
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Right_column_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + TC2_row_minY));
dt << run formulas;
,
Conversion_1 == 19, //From CADNAV frame
dt << New Column("DUMMY X", numeric, continuous, formula(:Name("CADNAV X") + Frame_minX));
dt << New Column("DUMMY Y", numeric, continuous, formula(:Name("CADNAV Y") + Frame_minY));
dt << run formulas;
);
:Name("DUMMY X") << Delete Formula;
:Name("DUMMY Y") << Delete Formula;

dt << New Column("Row", character, continuous, formula(If(
Frame_maxY < :Name("DUMMY Y") | Frame_minY > :Name("DUMMY Y"), "OOB", 
Bottom_row_minY <= :Name("DUMMY Y") <= Bottom_row_maxY, "Bottom", 
Middle_row_minY <= :Name("DUMMY Y") <= Middle_row_maxY, "Middle", 
Top_row_minY <= :Name("DUMMY Y") <= Top_row_maxY, "Top", 
TC1_row_minY <= :Name("DUMMY Y") <= TC1_row_maxY, "TC1x", 
TC2_row_minY <= :Name("DUMMY Y") <= TC2_row_maxY, "TC2x",  
"Frame")));
:Name("Row") << Delete Formula;

dt << New Column("Column", character, continuous, formula(If(
Frame_maxX < :Name("DUMMY X") | Frame_minX > :Name("DUMMY X"), "OOB", 
Left_column_minX <= :Name("DUMMY X") <= Left_column_maxX, "Left", 
Middle_column_minX <= :Name("DUMMY X") <= Middle_column_maxX, "Middle", 
Right_column_minX <= :Name("DUMMY X") <= Right_column_maxX, "Right", 
"Frame")));
:Name("Column") << Delete Formula;

dt << New Column("Seat", character, continuous, formula(If(
:Name("Row") == "OOB" | :Name("Column") == "OOB", "OOB", 
:Name("Row") == "Frame" | :Name("Column") == "Frame", "Frame", 
:Name("Row") == "TC1x" & :Name("Column") == "Left", "TC11", 
:Name("Row") == "TC1x" & :Name("Column") == "Middle", "TC12", 
:Name("Row") == "TC1x" & :Name("Column") == "Right", "TC13", 
:Name("Row") == "TC2x" & :Name("Column") == "Left", "TC21", 
:Name("Row") == "TC2x" & :Name("Column") == "Middle", "TC22", 
:Name("Row") == "TC2x" & :Name("Column") == "Right", "TC23",
:Name("Row") == "Bottom" & :Name("Column") == "Left", "ULV (left)", 
:Name("Row") == "Bottom" & :Name("Column") == "Middle", "ULV (middle)", 
:Name("Row") == "Bottom" & :Name("Column") == "Right", "ULV (right)", 
:Name("Row") == "Middle" & :Name("Column") == "Left", "YLD no DFM (left)", 
:Name("Row") == "Middle" & :Name("Column") == "Middle", "YLD no DFM (middle)", 
:Name("Row") == "Middle" & :Name("Column") == "Right", "YLD no DFM (right)", 
:Name("Row") == "Top" & :Name("Column") == "Left", "YLD DFM (left)", 
:Name("Row") == "Top" & :Name("Column") == "Middle", "YLD DFM (middle)", 
:Name("Row") == "Top" & :Name("Column") == "Right", "YLD DFM (right)")));
:Name("Seat") << Delete Formula;

If(
Conversion_2 == 1, //To CAMDEX
dt << New Column("CAMDEX X", numeric, continuous, formula(:Name("DUMMY X")));
dt << New Column("CAMDEX Y", numeric, continuous, formula(:Name("DUMMY Y")));
dt << run formulas;
:Name("CAMDEX X") << Delete Formula;
:Name("CAMDEX Y") << Delete Formula;
,
Conversion_2 == 2, //To CDSEM
dt << New Column("CDSEM X", numeric, continuous, formula(:Name("DUMMY X") + CDSEM_offset_X));
dt << New Column("CDSEM Y", numeric, continuous, formula(:Name("DUMMY Y") + CDSEM_offset_Y));
dt << run formulas;
:Name("CDSEM X") << Delete Formula;
:Name("CDSEM Y") << Delete Formula;
,
Conversion_2 == 3, //To KLARITY
dt << New Column("KLARITY X", numeric, continuous, formula(:Name("DUMMY X") + KLARITY_offset_X));
dt << New Column("KLARITY Y", numeric, continuous, formula(:Name("DUMMY Y") + KLARITY_offset_Y));
dt << run formulas;
:Name("KLARITY X") << Delete Formula;
:Name("KLARITY Y") << Delete Formula;
,
Conversion_2 == 4, //To CADNAV
dt << New Column("CADNAV X", numeric, continuous, formula(If(
:Name("Seat") == "Frame", :Name("DUMMY X") - Frame_minX,
:Name("Column") == "Left", :Name("DUMMY X") - Left_column_minX, 
:Name("Column") == "Middle", :Name("DUMMY X") - Middle_column_minX, 
:Name("Column") == "Right", :Name("DUMMY X") - Right_column_minX, 
Empty()
)));
dt << New Column("CADNAV Y", numeric, continuous, formula(If(
:Name("Seat") == "Frame", :Name("DUMMY Y") - Frame_minY, 
:Name("Row") == "Bottom", :Name("DUMMY Y") - Bottom_row_minY, 
:Name("Row") == "Middle", :Name("DUMMY Y") - Middle_row_minY, 
:Name("Row") == "Top", :Name("DUMMY Y") - Top_row_minY, 
:Name("Row") == "TC1x", :Name("DUMMY Y") - TC1_row_minY, 
:Name("Row") == "TC2x", :Name("DUMMY Y") - TC2_row_minY, 
Empty())));
dt << run formulas;
:Name("CADNAV X") << Delete Formula;
:Name("CADNAV Y") << Delete Formula;
);
dt << Delete Columns("DUMMY X", "DUMMY Y", "Row", "Column");
);

),
ButtonBox("Cancel",
	TextBox(" ")
)
)
);
);